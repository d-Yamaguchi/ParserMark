File                      = _ { $(ImportDeclaration)* ( $(TopLevel) / _ )* #Source } EOT
_                         = ( S / BLOCKCOMMENT / LINECOMMENT )*
S                         = [ \n\r\t\u000B\u000C]
BLOCKCOMMENT              = '/*' ( !'*/' . )* '*/'
LINECOMMENT               = '//' ( !'\n' . )*
EOT                       = !.
Name                      = { NAME #Name } _
NAME                      = !DIGIT !( KEYWORD !W ) W+
DIGIT                     = [0-9]
W                         = [a-zA-Z0-9_$]
TopLevel                  = Declaration
                          / { #Empty } ";"
KEYWORD                   = 'else'
                          / 'if'
                          / 'int'
                          / 'return'

example File '''
int fib(int n) {
  if(n < 3) return 1;
  return fib(n-1)+fib(n-2);
}
'''

// Declaration
ImportDeclaration         = "import" { $(Name ('.' Name)*) #ImportDecl}
Declaration               = MethodDeclaration
                          / VariableDeclaration
MethodDeclaration       = { $type(Type) _ $name(Name) _ "(" $list(MethodParamList) ")" (_ $body(Block) / _ ";") #MethodDecl }
                          / { $type(Type) _ $(Block) #MethodDecl }
MethodParamList           = { ( $(MethodParam) ( "," $(MethodParam) )* )? ( "," "..." )? #List }
MethodParam               = { $type(Type) _ $name(Name)? #Param } _
VariableDeclaration       = { $type(Type) _ $list(VariableList) ";" #Declaration }
VariableList              = { $(InitDecl) ( "," $(InitDecl) )*  #VarList}
InitDecl                  = { $name(Name) ( "=" $expr(Initializer) )? #VarDecl } _
Initializer               = AssignmentExpression

example Declaration int a = 0;
example VariableDeclaration ~0cf1e0 int a;
example VariableDeclaration ~83437a int a,b,c;
example VariableDeclaration ~ef2263 int a=0;
example VariableDeclaration ~4bd8c5 int a=0,b=1,c=2;

// Type
Type                      = PrimitiveType
                          / ReferenceType
PrimitiveType             = { "int" #TInt }
                          / { "boolean" #TBoolean }
                          / { "long" #TLong}
                          / { "string" #TString }
ReferenceType             = Name
// Block, Statement
Block                     = { "{" ( $(Statement) / $(Declaration) / _ )* "}" #Block }
Statement                 = Block
                          / { "if" _ "(" $cond(Expression) ")" $then(Block) ( "else" _ $else(Block) )? #If }
                          / { "return" _ $expr(Expression)? ";" #Return }
                          / { $expr(Expression) ";" #ExpressionStatement }
                          / { #Empty } ";"

example Block ~8909e9 '''
{
}
'''
example Block ~0038ad '''
{
a;
}
'''
example Block ~6bba1c '''
{
a=0;
}
'''
example Statement ~1591a2 '''
if(c){
}
'''

// Expression
Expression                = AssignmentExpression {$ "," $(AssignmentExpression) #Expression }*
AssignmentExpression      = { $left(UnaryExpression) _AssignmentOperator $right(AssignmentExpression) }
                          / ConditionalExpression
_AssignmentOperator       = "=" #Assign
ConstantExpression        = ConditionalExpression
ConditionalExpression     = EqualityExpression {$left ("||" #Or / "&&" #And) $right(EqualityExpression) }*
EqualityExpression        = RelationalExpression {$left ("==" #Equals / "!=" #NotEquals) $right(RelationalExpression) }*
RelationalExpression      = UnaryExpression {$left "<" #LessThan  $right(UnaryExpression) }*
UnaryExpression           = PostfixExpression
                          / { "!" $expr(UnaryExpression) #Not }
PostfixExpression         = PrimaryExpression {$recv _FunctionCall }*
_FunctionCall             = $({ "(" _ArgumentExpressionList? ")" #List }) #Apply
_ArgumentExpressionList   = $(AssignmentExpression) ( "," $(AssignmentExpression) )*
PrimaryExpression         = Constant
                          / "(" Expression ")"
                          / Name

//Literal
Constant                  = IntegerLiteral
                          / BooleanLiteral
                          / StringLiteral
                          / NullLiteral

IntegerLiteral            = { DECIMAL #Integer } _
DECIMAL                   = [1-9] DIGIT* / '0'
BooleanLiteral            = "true" { #True } _
                          / "false" { #False } _
StringLiteral             = '"' { STRING_CONTENT* #String } '"' _
STRING_CONTENT            = ![\"\n\\] .
NullLiteral               = 'null' { #Null} _


example Constant 1

"="                       = '=' !'=' _
"."                       = '.' _
"'"                       = '\'' _
"("                       = '(' _
")"                       = ')' _
"{"                       = '{' _
"["                       = '[' _
"]"                       = ']' _
","                       = ',' _
"..."                     = '...' _
":"                       = ':' ![>] _
"/"                       = '/' !'=' _
"<"                       = '<' ![=] _
">"                       = '>' ![=] _
"=="                      = '==' _
"!="                      = '!=' _
"&&"                      = '&&' _
"||"                      = '||' _
";"                       = ';' _
"}"                       = '}' _
"++"                      = '++' _
"--"                      = '--' _
"+"                       = '+' ![+=] _
"-"                       = '-' !( '-' / [=>] ) _
"*"                       = '*' _
"!"                       = '!' !'=' _
"string"                  = 'string' !W
"int"                     = 'int' !W
"boolean"                 = 'boolean' !W
"if"                      = 'if' !W
"for"                     = 'for' !W
"else"                    = 'else' !W
"return"                  = 'return' !W
"false"                   = 'false' !W
"true"                    = 'true' !W
"import"                  = 'import' !W
"long"                    = 'long' !W



example Statement '''
return 1;
'''
example Expression ~46f038 '''
x=y
'''
example Expression ~8e03b0 '''
x+=y
'''
example Expression ~4e09fb '''
c?x:y
'''
example Expression ~7ac13c '''
x<<y
'''
example Expression ~9b2696 '''
x>>y
'''
example Expression ~dea67f '''
x|y
'''
example Expression ~7f3ecc '''
x&y
'''
example Expression ~78b953 '''
x^y
'''
example Expression ~f21916 '''
~x
'''
example Expression ~006e0a '''
x&&y
'''
example Expression ~b60455 '''
x||y
'''
example Expression ~51b2b2 '''
!x
'''
example Expression ~b0be29 '''
x==y
'''
example Expression ~f89dae '''
x!=y
'''
example Expression ~ad9f44 '''
x<y
'''
example Expression ~37ebeb '''
x<=y
'''
example Expression ~ae662a '''
x+y
'''
example Expression ~fa8a6a '''
x*y
'''

// formatted by $ nez format
